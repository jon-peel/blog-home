+++
title = "AoC 2023, Day 1, Trebuchet"
author = ["Jonathan Peel"]
date = 2023-12-01T08:06:00+03:00
tags = ["blog", "AoC", "advent", "cod", "fsharp"]
draft = false
+++

It is the first of December, and that means the start of [Advent of Code](https://adventofcode.com/2023).
I am going to make an attempt at this every day, posting my coding solutions (as much as possible in F#). I will not be posting the questions, as they can be found online, and I will not be posting my final answer.

[The first question is called _Trebuchet_](https://adventofcode.com/2023/day/1).


## Part 1 {#part-1}

I will first need a function that will be able to grab the first and last digits from a single row string.

{{< highlight fsharp >}}
let firstAndLastDigit (codedCalibration: string) =
    let digits =
        codedCalibration
        |> Seq.where (System.Char.IsDigit)
        |> Seq.toArray
    [| digits[0]; Seq.last digits |]
    |> System.String
    |> int

firstAndLastDigit "1abc2"
{{< /highlight >}}

```text
12
```

This might be enough to put it all together.

{{< highlight fsharp >}}
let exampleInput =
    "1abc2
pqr3stu8vwx
a1b2c3d4e5f
treb7uchet"

exampleInput.Split '\n'
|> Seq.map firstAndLastDigit
|> Seq.sum
{{< /highlight >}}

This will have a final result of \`142\`.


## Part 2 {#part-2}

To complete part two, I want to first get a function that will extract all digits, and digit words out from a string.

{{< highlight fsharp >}}
let (|StartsWith|_|) (start: string) (full: string)  =
    if full.StartsWith(start) then Some full
    else None

let extract (input: string) =
    match input with
    | StartsWith "zero" _ -> Some 2
    | StartsWith "one" _ -> Some 1
    | StartsWith "two" _ -> Some 2
    | StartsWith "three" _ -> Some 3
    | StartsWith "four" _ -> Some 4
    | StartsWith "five" _ -> Some 5
    | StartsWith "six" _ -> Some 6
    | StartsWith "seven" _ -> Some 7
    | StartsWith "eight" _ -> Some 8
    | StartsWith "nine" _ -> Some 9
    | x when System.Char.IsDigit x[0] -> Some (int x[0..0])
    | _ -> None

let digitsOf (input: string) =
    seq { for i = 0 to input.Length - 1 do
          yield (extract input[i..]) }
    |> Seq.choose id
    |> Seq.toArray
{{< /highlight >}}

With the input of this will return

<a id="code-snippet--aoc-2023-01-b-1"></a>
{{< highlight fsharp >}}
digitsOf "two1nine"
{{< /highlight >}}

This will return

{{< highlight text >}}
| 2 | 1 | 9 |
{{< /highlight >}}

And now I can update the original function to use this

{{< highlight fsharp >}}
let firstAndLastDigit (codedCalibration: string) =
    let digits = digitsOf codedCalibration
    [| digits[0]; Seq.last digits |]
    |> Array.map string
    |> (fun s -> System.String.Join ("", s))
    |> int

firstAndLastDigit "two1nine"
{{< /highlight >}}

```text
29
```

And then I can test with the test input

{{< highlight fsharp >}}
let exampleInput =
    "two1nine
eightwothree
abcone2threexyz
xtwone3four
4nineeightseven2
zoneight234
7pqrstsixteen"

exampleInput.Split '\n'
|> Seq.map firstAndLastDigit
|> Seq.sum
{{< /highlight >}}

This returns \`281\` which is the correct result for the test input.

```text
281
```
