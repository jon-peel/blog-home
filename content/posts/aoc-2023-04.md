+++
title = "AoC 2023, Day 4, Scratchcards"
author = ["Jonathan Peel"]
date = 2023-12-05T08:37:00+03:00
tags = ["blog", "Aoc", "advent", "code", "fsharp"]
draft = false
+++

Today's puzzle is called [Scratchcards](https://adventofcode.com/2023/day/4).


## Part 1 {#part-1}

I am going to start by creating a type called card to store the information in.

{{< highlight fsharp >}}
type Card = Card of no: int * winners: int [] * mine: int []
{{< /highlight >}}

I need to be able to parse the card text, This could be done more elegant, but I'm going to split the string up, and assume there is no problem in the input data.

{{< highlight fsharp >}}
module Card =
    let parse (text: string) =
        let cleanSplit (text: string) (chr: char) =
            text.Split(chr) |> Array.filter (fun x -> x.Length > 0)
        let toNumArray (text: string) =
            cleanSplit text ' '
            |> Array.map (fun x -> x.Trim())
            |> Array.filter (fun x -> x.Length > 0)
            |> Array.map int

        let cardNoSplit = text.Split(':')
        let no = (cleanSplit cardNoSplit[0] ' ').[1].Trim() |> int
        let numbersSplit = cardNoSplit[1].Split('|')
        let winners = toNumArray numbersSplit[0]
        let mine = toNumArray numbersSplit[1]
        Card (no, winners, mine)
{{< /highlight >}}

I can test this on the first row.

<a id="code-snippet--aoc-2023-04-test1"></a>
{{< highlight fsharp >}}
Card.parse "Card 1: 41 48 83 86 17 | 83 86  6 31 17  9 48 53"
{{< /highlight >}}

Which returns:

```text
Card.parse "Card 1: 41 48 83 86 17 | 83 86  6 31 17  9 48 53";;
val it: Card =
  Card (1, [|41; 48; 83; 86; 17|], [|83; 86; 6; 31; 17; 9; 48; 53|])
```

I now need to score a card, let me first get the number of winning numbers in my numbers.

{{< highlight fsharp >}}
module Card =
    let winningNumbers (Card (_, winners, mine)) =
        [| for no in mine do
           match (winners |> Seq.tryFind ((=) no)) with
           | Some x -> yield x
           | _ -> () |]
{{< /highlight >}}

I will test this.

<a id="code-snippet--aoc-2023-04-test2"></a>
{{< highlight fsharp >}}
"Card 2: 13 32 20 16 61 | 61 30 68 82 17 32 24 19"
|> Card.parse
|> Card.winningNumbers
{{< /highlight >}}

Which returns:

{{< highlight text >}}
| 61 | 32 |
{{< /highlight >}}

{{< highlight fsharp >}}
module Card =
    let score card =
        let rec double acc numbers =
            match numbers with
            | [||] -> acc
            | _ -> double (if acc = 0 then 1 else acc * 2) numbers[1..]
        double 0 (Card.winningNumbers card)
{{< /highlight >}}

I will test this.

<a id="code-snippet--aoc-2023-04-test3"></a>
{{< highlight fsharp >}}
"Card 3:  1 21 53 59 44 | 69 82 63 72 16 21 14  1"
|> Card.parse
|> Card.score
{{< /highlight >}}

Which returns:

{{< highlight text >}}
2
{{< /highlight >}}

I am happy this far, lets pipe it all together and see what we get.

<a id="code-snippet--aoc-2023-04-test4"></a>
{{< highlight fsharp >}}
let sampleText =
    "Card 1: 41 48 83 86 17 | 83 86  6 31 17  9 48 53
Card 2: 13 32 20 16 61 | 61 30 68 82 17 32 24 19
Card 3:  1 21 53 59 44 | 69 82 63 72 16 21 14  1
Card 4: 41 92 73 84 69 | 59 84 76 51 58  5 54 83
Card 5: 87 83 26 28 32 | 88 30 70 12 93 22 82 36
Card 6: 31 18 13 56 72 | 74 77 10 23 35 67 36 11"

sampleText.Split('\n')
|> Seq.map Card.parse
|> Seq.map Card.score
|> Seq.sum
{{< /highlight >}}

{{< highlight text >}}
13
{{< /highlight >}}

`13` is the correct answer for the sample data. <br />
My final answer is also correct, so I earn another gold star.
