+++
title = "AoC 2023, Day 6, Wait for it"
author = ["Jonathan Peel"]
date = 2023-12-17T12:39:00+03:00
tags = ["blog", "AoC", "advent", "code", "fsharp", "units-of-measure"]
draft = false
+++

This puzzle is called [Wait For It](https://adventofcode.com/2023/day/6).

{{< highlight fsharp >}}
open System

let sampleText =
    "Time:      7  15   30
Distance:  9  40  200"
{{< /highlight >}}


## Part 1 {#part-1}

In this puzzle, I can make use of units of measure.
This will allow me to make sure I don't get my time numbers mixed up with my distance numbers. <br />
Because F# does not have built-in units for milliseconds and milliseconds I can very easily define them myself.

{{< highlight fsharp >}}
[<Measure>] type ms
[<Measure>] type mm
{{< /highlight >}}

I also want a type to hold the basic data for each rice that is extracted while parsing.

{{< highlight fsharp >}}
type Race = Race of time: int64<ms> * distance: int64<mm>
{{< /highlight >}}

I need a couple of functions to help me pass the text into my `Race` type.

{{< highlight fsharp >}}
let pasreAs (measure: int64<'a>) (txt: string) =
        txt.Split(':').[1].Trim().Split(' ')
        |> Array.where (fun s -> s <> "")
        |> Array.map int64
        |> Array.map ((*) measure)

let parseRases (text: string) =
    let [|t; d|] = text.Split('\n')
    let times = t |> pasreAs 1L<ms>
    let dists = d |> pasreAs 1L<mm>
    [ for i = 0 to times.Length - 1 do
        yield Race (times[i], dists[i])]
{{< /highlight >}}

The next function will calculate the distance the boat will travel, given the time it spends accelerating and time is spends moving.

{{< highlight fsharp >}}
let distanceTraveld accelerate time =
    let speed = accelerate * 1L<mm/ms^2>
    let timeToRace = time - accelerate
    let distance = speed * timeToRace
    distance
{{< /highlight >}}

I need to calculate each possible travel distance for each race.

{{< highlight fsharp >}}
let allAcceleratorOptions (time: int64<ms>) =
    seq { for i = 1L to (time * 1L</ms>) do
          yield distanceTraveld (i*1L<ms>) time }
{{< /highlight >}}

I must count how many of each possible travel distances would win

{{< highlight fsharp >}}
let winCount (Race (time, distance)) =
    allAcceleratorOptions time
    |> Seq.filter (fun x -> distance < x)
    |> Seq.length
{{< /highlight >}}

Let me run this, and see what I get.

{{< highlight fsharp >}}
sampleText
|> parseRases
|> List.map winCount
|> List.fold (*) 1
{{< /highlight >}}

```text
288
```

`288` is the expected value for the test data. <br />
My final answer is also correct, and I get another star.


## Part 2 {#part-2}

In part two, most calculations are exactly the same.
We rather just has to run it as a single race, rather than a collection of races.

I will create a couple of more functions to parse the input text as a single race.

{{< highlight fsharp >}}
let parseAsOne (measure: int64<'a>) (txt: string) =
    txt.Split(':').[1].Replace(" ", "").Trim()
    |> int64
    |> ((*) measure)

let parseOneRace (text: string) =
    let [|t; d|] = text.Split('\n')
    let time = t |> parseAsOne 1L<ms>
    let dist = d |> parseAsOne 1L<mm>
    Race (time, dist)
{{< /highlight >}}

I will then run this, and see what I get.

{{< highlight fsharp >}}
sampleText
|> parseOneRace
|> winCount
{{< /highlight >}}

```text
71503
```

`71503` is correct, and I get another star for my final answer as well.
